# This is a proof of concept presentation file
# for the Sly markup language
#
# The goal for this project is to produce working
# slideshows from textual formats
#
# Using slydes, the Sly processing tool, this presentation
# can be viewed either in a native client or exported as
# either HTML, PDF, or (eventually) PPT

# Setup our color palette
let coolGray = (26, 83, 92);
mut paleGreen = (247, 255, 247);
let tealBlue = (78, 205, 196);

macro titleStyle() {
    self.font = "Fira Code";
    self.fontSize = 42;
    self.fontColor = tealBlue;
    self.justify = "center";
}

macro contentStyle() {
    self.justify = "left";
    self.font = "Times New Roman";
    self.fontSize = 32;
    self.fontColor = paleGreen;
}

slide intro {
    self.backgroundColor = coolGray;

    block title {
        $titleStyle();

        ---
        Welcome!

        This is an example of Slydes
        ---
    }
}

slide firstSlide : intro {
    block title {
        $titleStyle();
        ---These are my thoughts---
    }

    block pointOne {
        $contentStyle();
        --- - This is my first point ---
    }

    block pointTwo : pointOne {
        --- - This is my second point ---
    }
}

slide secondSlide : intro {
    block title {
        $titleStyle();
        ---How about this?---
    }

    block body1 {
        $contentStyle();

        self.justify = "right";
        self.fontSize = 38;

---





I'm boldly making my point---
    }

    block body2 : body1 {
        self.fontColor = "red";
        self.justify = "center";
        ---With some style---
    }

    block body3 : body2 {
        self.justify = "left";
        ---What do you think?---
    }
}

slide conclusion : intro {
    block title {
        $titleStyle();
---



We're through
---
    }
}